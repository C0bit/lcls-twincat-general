<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.12">
  <POU Name="FB_EcatDiag" Id="{04266cc1-1752-4ae8-b692-47df0663f314}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_EcatDiag
VAR_INPUT
	{attribute 'naming' := 'omit'}
	i_AMSNetId AT %I* : AMSNETID; //Link to the AMSNETID name in the ethercat master info.
	i_xFirstPass: BOOL;
END_VAR
VAR_OUTPUT
	q_xAllSlaveStatesGood: BOOL; // Set to True if all Slaves are in OP State
	q_aiTermStates: ARRAY[1..256] OF BYTE; 
	q_xMasterStateGood:BOOL; // Set to True if the Master Device State is OP
	q_iMasterState: WORD; // The Device State of the Master
	q_sMasterState:STRING;
	q_aEcConfSlaveInfo :  ARRAY[1..256] OF ST_EcDevice;
	q_nSlaves: UINT; // the Number of the connected Slaves
END_VAR
VAR
	sNetId: T_AmsNetId;
	astTermStates: ARRAY[1..256] OF ST_EcSlaveState;
	astEcConfSlaveInfo: ARRAY[1..256] OF ST_EcSlaveConfigData;
	fbGetAllSlaveStates: FB_EcGetAllSlaveStates;
	fbGetSlaveCount: FB_EcGetSlaveCount;
	fbGetMasterState: FB_EcGetMasterState;
	fbGetConfSlaves: FB_EcGetConfSlaves;
	{attribute 'naming' := 'omit'} 
	ftReset: F_TRIG;
	{attribute 'naming' := 'omit'}
	ftMasterReset: F_TRIG;
	nIterator: INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* 
Ecat bus diagnostic tool
2015-11-4 Alex Wallace
This function block checks the states of all slaves on the ecat bus network, 
it could be modified to export the states of the slaves on an individual basis,
but for now it sets the output boolean true if all slaves are OP and false otherwise.
To start the block provide a falling edge on the first pass boolean input.

2018-05-05 Margaret Ghaly
Function block has been modified to retrieve the Device State of the Ethercat Master.
It also exports the states and information of each individual configured Slave. 
And saves them in the array q_aEcConfSlaveInfo.
*)



//Create the net ID string
sNetId := F_CreateAmsNetId(i_AMSNetId);

//Query the state of all terminals, collect in astTermStates
ftReset(CLK:=fbGetAllSlaveStates.bBusy OR i_xFirstPass);
fbGetAllSlaveStates.bExecute := ftReset.Q;
fbGetAllSlaveStates(sNetId:=sNetId, pStateBuf := ADR(astTermStates), cbBufLen:=SIZEOF(astTermStates));
//Keep checking...



//Cycle through each entry in the array and check if we have anyone not in OP and that the link state is good.
// If so, then set our global IO bad boolean.
IF fbGetAllSlaveStates.nSlaves > 0 THEN
	q_xAllSlaveStatesGood := TRUE;
FOR nIterator := 1 TO (UINT_TO_INT(fbGetAllSlaveStates.nSlaves) ) BY 1
	DO
	IF NOT( (astTermStates[nIterator].deviceState = EC_DEVICE_STATE_OP) AND (astTermStates[nIterator].linkState = EC_LINK_STATE_OK)) THEN
		q_xAllSlaveStatesGood := FALSE;
	END_IF
	q_aiTermStates[nIterator] := astTermStates[nIterator].deviceState;
	q_aEcConfSlaveInfo[nIterator].nDeviceState :=astTermStates[nIterator].deviceState;//
	q_aEcConfSlaveInfo[nIterator].nLinkState :=astTermStates[nIterator].linkState;//
	q_aEcConfSlaveInfo[nIterator].sDeviceState:= F_ConvSlaveStateToString(state:=astTermStates[nIterator]);//
	
END_FOR
END_IF

// Read the EtherCAT state of the master. If the call is successful, 
//the State output variable of type WORD contains the requested status information.
ftMasterReset(CLK:=fbGetMasterState.bBusy OR i_xFirstPass);
fbGetMasterState(sNetId:= sNetId, bExecute:=ftMasterReset.Q, 
				state => q_iMasterState,bError=>,
				nErrId=>);
q_xMasterStateGood:= (fbGetMasterState.state = BYTE_TO_UINT(EC_DEVICE_STATE_OP));
q_sMasterState := F_ConvMasterDevStateToString(fbGetMasterState.state);

//This function is used to read a list of all configured slaves from the EtherCat master object Directory
//needs to run only once
fbGetConfSlaves(bExecute := i_xFirstPass, sNetId :=sNetId, pArrEcConfSlaveInfo := ADR(astEcConfSlaveInfo),cbBufLen := SIZEOF(astEcConfSlaveInfo));
q_nSlaves:=fbGetConfSlaves.nSlaves;

IF  NOT (fbGetConfSlaves.bBusy) THEN
	FOR nIterator := 1 TO (UINT_TO_INT(fbGetConfSlaves.nSlaves) ) BY 1
	DO
	q_aEcConfSlaveInfo[nIterator].nAddrr :=astEcConfSlaveInfo[nIterator].nAddr;
	q_aEcConfSlaveInfo[nIterator].sName :=astEcConfSlaveInfo[nIterator].sName;
	q_aEcConfSlaveInfo[nIterator].sType :=astEcConfSlaveInfo[nIterator].sType;
END_FOR
	fbGetConfSlaves.bExecute := FALSE;
END_IF



]]></ST>
    </Implementation>
    <LineIds Name="FB_EcatDiag">
      <LineId Id="540" Count="69" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>